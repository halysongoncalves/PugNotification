apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

version VERSION_NAME
group POM_GROUP_ID

publishing {
    publications {
        Production(MavenPublication) {
            artifact("$buildDir/outputs/aar/library-release.aar")
            groupId this.group
            artifactId POM_ARTIFACT_ID
            version this.version

            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')

                // Iterate over the implementation dependencies (we don't want the test ones), adding a <dependency> node for each
                configurations.implementation.allDependencies.each {
                    // Ensure dependencies such as fileTree are not included in the pom.
                    if (it.name != 'unspecified') {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }
    }
}

bintray {
    user = bintrayUser
    key = bintrayApiKey
    publications = ['Production']
    configurations = ['archives']
    override = true
    pkg {
        repo = 'maven-release'
        name = 'Pugnotification'
        description = POM_DESCRIPTION
        publish = true
        publicDownloadNumbers = true
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/Plinzen/Pugnotification.git'
        dryRun = false
        version {
            name = this.version
            desc = "Pugnotification ${this.version}"
            released = new Date()
            vcsTag = this.version
        }
    }
}